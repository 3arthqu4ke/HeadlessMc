apply plugin: 'headlessmc-modules'

repositories {
    mavenCentral()
    maven {
        url 'https://libraries.minecraft.net/'
    }
}

module {
    name = 'headlessmc.lwjgl'
    access = ACC_OPEN

    exports 'me/earth/headlessmc/lwjgl'
    exports 'me/earth/headlessmc/lwjgl/api'
    exports 'me/earth/headlessmc/lwjgl/transformer'
    exports 'me/earth/headlessmc/lwjgl/redirections'
    exports 'me/earth/headlessmc/lwjgl/util'
    exports 'me/earth/headlessmc/lwjgl/agent'

    requires 'java.desktop'
    requires 'java.instrument', ACC_STATIC
    requires 'org.objectweb.asm.tree', ACC_STATIC
    requires 'org.objectweb.asm', ACC_STATIC
}

configurations {
    // make launchwrapper available to tests
    testImplementation.extendsFrom compileOnly
}

dependencies {
    compileOnly(group: 'net.minecraft', name: 'launchwrapper', version: '1.12') {
        exclude module: 'asm-debug-all'
        // exclude unsafe log4j 2.0.9
        exclude module: 'log4j-api'
        exclude module: 'log4j-core'
        // We don't exclude the LWJGL dependency because even though it's
        // outdated, it can be nice to get an idea of what some Lwjgl classes
        // look like when debugging the LWJGL instrumentation.
    }

    implementation group: 'org.ow2.asm', name: 'asm', version: '9.3'
    implementation group: 'org.ow2.asm', name: 'asm-tree', version: '9.4'

    testImplementation(testFixtures(project(':headlessmc-commons')))
    // Only for the LaunchWrapperTest, which is skipped on Java > 8
    if (JavaVersion.current() <= JavaVersion.VERSION_1_8) {
        testRuntimeOnly 'org.apache.logging.log4j:log4j-api:2.19.0'
        testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.18.0'
    }
}

jar {
    archivesBaseName = 'headlessmc-lwjgl'
    //noinspection GroovyAssignabilityCheck
    manifest {
        attributes 'TweakClass': 'me.earth.headlessmc.lwjgl.launchwrapper.LwjglTweaker'
        attributes 'Premain-Class': 'me.earth.headlessmc.lwjgl.agent.LwjglAgent'
        attributes 'Agent-Class': 'me.earth.headlessmc.lwjgl.agent.LwjglAgent'
        attributes 'Can-Redefine-Classes': true
        attributes 'Can-Retransform-Classes': true
    }
}

task testAgent(type: Test, dependsOn: jar) {
    group = 'verification'
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
    }

    jvmArgs = [
        "-javaagent:" + projectDir + "/build/libs/headlessmc-lwjgl.jar",
        '-Dhmc.lwjgl.agenttest=true'
    ]

    filter {
        includeTestsMatching 'me.earth.headlessmc.lwjgl.agent.AgentTest'
    }
}
