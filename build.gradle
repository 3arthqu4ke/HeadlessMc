group 'me.earth.headlessmc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    //noinspection GroovyAssignabilityCheck
    plugins.withType(JavaPlugin) {
        repositories {
            mavenCentral()
            maven {
                url 'https://jitpack.io'
            }
        }

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        compileJava {
            if (JavaVersion.current() > JavaVersion.VERSION_1_8) {
                options.compilerArgs.addAll(['--release', '8'])
            }
        }

        configurations {
            jarLibs
        }

        dependencies {
            compileOnly 'org.projectlombok:lombok:1.18.22'
            annotationProcessor 'org.projectlombok:lombok:1.18.22'

            testCompileOnly 'org.projectlombok:lombok:1.18.22'
            testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

            implementation configurations.jarLibs
        }

        test {
            useJUnitPlatform()
            testLogging {
                events "failed"
                exceptionFormat "full"
            }

            finalizedBy jacocoTestReport
        }

        jacocoTestReport {
            dependsOn test
        }

        jar {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            from {
                configurations.jarLibs.collect {
                    it.isDirectory() ? it : zipTree(it)
                }
            }
        }
    }
}

task jacocoRootReport(type: JacocoReport, group: 'verification') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(subprojects.test)

    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    executionData.from = files(subprojects.jacocoTestReport.executionData)

    reports {
        html.enabled true
        xml.enabled true
    }
}

task copyJars(type: Copy, dependsOn: subprojects.jar) {
    from(subprojects.jar)
    into project.file(System.getProperty('hmc.jar.dir', 'build/libs'))
}

jar {
    dependsOn(subprojects.jar)
    finalizedBy(copyJars)
}

// the launchers report will otherwise contain coverage data about some of the
// libraries we use in those two jars, e.g. xdarks Deencapsulation
tasks.withType(JacocoReport) {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/headlessmc/headlessmc-runtime.jar',
                '**/headlessmc/headlessmc-lwjgl.jar'
            ])
        }))
    }
}
